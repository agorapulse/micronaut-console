/*
 * SPDX-License-Identifier: Apache-2.0
 *
 * Copyright 2020-2025 Agorapulse.
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     https://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */
plugins {
    id 'org.kordamp.gradle.groovy-project'
    id 'org.kordamp.gradle.checkstyle'
    id 'org.kordamp.gradle.codenarc'
    id 'org.kordamp.gradle.coveralls'
}

if (!project.hasProperty('mavenCentralUsername'))       ext.mavenCentralUsername            = System.getenv('MAVEN_CENTRAL_USERNAME') ?: '**UNDEFINED**'
if (!project.hasProperty('mavenCentralPassword'))       ext.mavenCentralPassword            = System.getenv('MAVEN_CENTRAL_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signingInMemoryKeyId'))       ext.signingInMemoryKeyId            = System.getenv('SIGNING_KEY_ID') ?: '**UNDEFINED**'
if (!project.hasProperty('signingInMemoryKeyPassword')) ext.signingInMemoryKeyPassword      = System.getenv('SIGNING_PASSWORD') ?: '**UNDEFINED**'
if (!project.hasProperty('signingInMemoryKey'))         ext.signingInMemoryKey              = System.getenv('SIGNING_SECRET_KEY_PATH') ? rootProject.file(System.getenv('SIGNING_SECRET_KEY_PATH')).text : '**UNDEFINED**'

config {
    release = (rootProject.findProperty('release') ?: false).toBoolean()

    info {
        name        = 'Micronaut Console'
        vendor      = 'Agorapulse'
        description = 'Micronaut Console'

        links {
            website      = "https://github.com/" + slug
            issueTracker = "https://github.com/" + slug + "/issues"
            scm          = "https://github.com/" + slug + ".git"
        }

        people {
            person {
                id    = 'musketyr'
                name  = 'Vladimir Orany'
                roles = ['developer']
            }
        }

        repositories {
            repository {
                name = 'localRelease'
                url  = "" + project.rootProject.buildDir + "/repos/local/release"
            }
            repository {
                name = 'localSnapshot'
                url  = "" + project.rootProject.buildDir + "/repos/local/snapshot"
            }
        }
    }

    licensing {
        licenses {
            license {
                id = 'Apache-2.0'
            }
        }
    }

    publishing {
        enabled = false
        signing {
            enabled = false
        }
        releasesRepository  = 'localRelease'
        snapshotsRepository = 'localSnapshot'
    }

    artifacts {
        source {
            enabled = false
        }
    }

    quality {
        checkstyle {
            toolVersion = '8.27'
        }

        codenarc {
            toolVersion = '1.5'
        }
    }

    coverage {
        coveralls {
            enabled = true
        }
    }

    docs {
        groovydoc {
            enabled = false
            aggregate {
                enabled = false
            }
        }

        javadoc {
            aggregate {
                enabled = false
            }
        }
    }

}

allprojects {
    repositories {
        mavenCentral()
        maven { url "https://repo.spring.io/release"  }
    }

    license {
        exclude '**/*.json'
        exclude '***.yml'
        exclude '***.js'
        exclude '**/ConsoleHandlerSpec/*.groovy'
        exclude '**/ConsoleHandlerSpec/*.txt'
        exclude '**/AuthConsoleHandlerSpec/*.groovy'
        exclude '**/AuthConsoleHandlerSpec/*.txt'
        exclude '**/ConsoleControllerSpec/*.groovy'
        exclude '**/ConsoleControllerSpec/*.txt'
        exclude 'external.groovy'
        exclude '**/scripts/*.groovy'
    }
}

gradleProjects {
    subprojects {
        dirs(['libs']) { Project subproject ->
            if (subproject.name == 'guide') return

            apply plugin: 'groovy'

            java {
                toolchain {
                    languageVersion.set(JavaLanguageVersion.of(17))
                }
            }
            // useful for Micronaut
            tasks.withType(GroovyCompile) {
                groovyOptions.forkOptions.jvmArgs.add('-Dgroovy.parameters=true')
            }

            // useful for Micronaut
            tasks.withType(JavaCompile){
                options.encoding = "UTF-8"
                options.compilerArgs.add('-parameters')
            }

            // location independent tests (useful for stable CI builds)
            tasks.withType(Test){
                useJUnitPlatform()

                systemProperty 'user.timezone', 'UTC'
                systemProperty 'user.language', 'en'
            }

            // useful for IntelliJ
            task cleanOut(type: Delete) {
                delete file('out')
            }

            clean.dependsOn cleanOut

            mavenPublishing {
                publishToMavenCentral(true)
                signAllPublications()

                pom {
                    name = "Micronaut Console"
                    description = 'Micronaut Console'
                    inceptionYear = "2020"
                    url = "https://github.com/${slug}"
                    licenses {
                        license {
                            name = "The Apache License, Version 2.0"
                            url = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                            distribution = "https://www.apache.org/licenses/LICENSE-2.0.txt"
                        }
                    }
                    developers {
                        developer {
                            id = "musketyr"
                            name = "Vladimir Orany"
                            url = "https://github.com/musketyr/"
                        }
                    }
                    scm {
                        url = "https://github.com/${slug}.git"
                        connection = "scm:git:git://github.com/${slug}.git"
                        developerConnection = "scm:git:ssh://git@github.com/${slug}.git"
                    }
                }
            }

            signing {
                useInMemoryPgpKeys(rootProject.ext.signingInMemoryKey, rootProject.ext.signingInMemoryKeyPassword)
                sign publishing.publications
            }
        }
    }
}

check.dependsOn('aggregateCheckstyle', 'aggregateCodenarc', 'aggregateAllTestReports', 'coveralls')
